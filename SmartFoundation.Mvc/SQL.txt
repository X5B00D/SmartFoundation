-- جدول الباسووردات 

USE [DATACORE]
GO

/****** Object:  Table [dbo].[userPassword]    Script Date: 16/11/2025 10:33:49 م ******/
SET ANSI_NULLS ON
GO

SET QUOTED_IDENTIFIER ON
GO

CREATE TABLE [dbo].[userPassword](
	[userPasswordID] [int] IDENTITY(1,1) NOT NULL,
	[IDNumber_FK] [nvarchar](20) NOT NULL,
	[PasswordHash] [varbinary](64) NOT NULL,
	[PasswordSalt] [varbinary](32) NOT NULL,
	[HashAlgorithm] [nvarchar](20) NULL,
	[userPasswordStartDate] [date] NULL,
	[userPasswordEndDate] [date] NULL,
	[userPasswordActive] [bit] NULL,
	[entryDate] [datetime] NULL,
	[entryData] [nvarchar](20) NULL,
	[hostName] [nvarchar](200) NULL,
 CONSTRAINT [PK_userPassword] PRIMARY KEY CLUSTERED 
(
	[userPasswordID] ASC
)WITH (PAD_INDEX = OFF, STATISTICS_NORECOMPUTE = OFF, IGNORE_DUP_KEY = OFF, ALLOW_ROW_LOCKS = ON, ALLOW_PAGE_LOCKS = ON, OPTIMIZE_FOR_SEQUENTIAL_KEY = OFF) ON [PRIMARY]
) ON [PRIMARY]
GO

ALTER TABLE [dbo].[userPassword] ADD  CONSTRAINT [DF_userPassword_HashAlgorithm]  DEFAULT ('SHA2_256') FOR [HashAlgorithm]
GO

ALTER TABLE [dbo].[userPassword] ADD  CONSTRAINT [DF_userPassword_entryDate]  DEFAULT (getdate()) FOR [entryDate]
GO

ALTER TABLE [dbo].[userPassword] ADD  CONSTRAINT [DF_userPassword_hostName]  DEFAULT (host_name()) FOR [hostName]
GO




-- تعديل بروسيجر بيانات المستخدم

USE [DATACORE]
GO

/****** Object:  StoredProcedure [dbo].[GetSessionInfoForMVC]    Script Date: 16/11/2025 11:14:29 م ******/
SET ANSI_NULLS ON
GO

SET QUOTED_IDENTIFIER ON
GO


CREATE   PROCEDURE [dbo].[GetSessionInfoForMVC]
    @UserID Nvarchar(20),
    @Password Nvarchar(200),
    @hostName Nvarchar(200)

AS
BEGIN
    SET NOCOUNT ON;


    DECLARE @Salt VARBINARY(32);
    DECLARE @StoredHash VARBINARY(64);
    DECLARE @PasswordResult int

  -- هل يوجد كلمة مرور للمستخدم؟
IF NOT EXISTS (
    SELECT 1 
    FROM DATACORE.dbo.userPassword us
    WHERE us.IDNumber_FK = @UserID and us.userPasswordActive = 1
)
BEGIN
    SET @PasswordResult = 3;   -- لا يوجد كلمة مرور
END
ELSE
BEGIN
    -- جلب الهاش والسولت
    SELECT 
        @Salt = PasswordSalt,
        @StoredHash = PasswordHash
    FROM DATACORE.dbo.userPassword
    WHERE IDNumber_FK = @UserID;

    -- فحص كلمة المرور
    IF @StoredHash = HASHBYTES('SHA2_256', @Salt + CAST(@Password AS VARBINARY(200)))
    BEGIN
        SET @PasswordResult = 1;  -- كلمة المرور صحيحة
    END
    ELSE
    BEGIN
        SET @PasswordResult = 0;  -- كلمة المرور خاطئة
    END
END

    DECLARE @GeneralNo bigint
         if(select count(us.userID) from DATACORE.dbo.[User] us where us.IDNumber = @UserID) > 0
             begin
                 set @GeneralNo = (select TOP(1) us.userID from DATACORE.dbo.[User] us where us.IDNumber = @UserID order by us.entryDate desc)
             END
         ELSE
             BEGIN
                 set @GeneralNo = -99
             END
    -- Create a temporary table to store the department info
    DECLARE @DepartmentInfo TABLE (
        UserID bigINT,
        DepartmentName NVARCHAR(255),
		DeptCode nvarchar(20),
		IdaraID INT
    );


	  DECLARE @ThameInfo TABLE (
        UserID bigINT,
        ThameName NVARCHAR(255)
    );
    
    -- Get the department info first
    INSERT INTO @DepartmentInfo (UserID, DepartmentName, DeptCode,IdaraID)
    SELECT TOP 1 
        udsd.userID, 
        dsd.DepartmentName,
		d.deptCode,
		d.idaraID_FK
    FROM dbo.V_GetFullStructureForDSD dsd
    INNER JOIN dbo.V_GetListUsersInDSD udsd ON dsd.DSDID = udsd.DSDID
	INNER JOIN dbo.Department d ON dsd.DepartmentID = d.deptID
    WHERE udsd.userID = @GeneralNo;


	INSERT INTO @ThameInfo (UserID, ThameName)
            SELECT TOP 1 
                   @GeneralNo,
                   ISNULL(
                        m.MvcThameName,
                        'default'
                   ) AS ThameName
            FROM DATACORE.dbo.MvcThameUser u
            LEFT JOIN DATACORE.dbo.MvcThame m 
                   ON u.MvcThameID_FK = m.MvcThameID
            WHERE u.UserID_FK = @GeneralNo
              AND u.MvcThameUserActive = 1
              AND m.MvcThameActive = 1;

                -- إذا لم يرجع السطر السابق أي نتيجة → إدراج default يدويًا
            IF NOT EXISTS (SELECT 1 FROM @ThameInfo WHERE UserID = @GeneralNo)
            BEGIN
                INSERT INTO @ThameInfo (UserID, ThameName)
                VALUES (@GeneralNo, 'default');
            END


     

    
    -- Combine the user and department information along with photo from Payroll database
    if(@GeneralNo = -99)
    
     BEGIN
          select
            0 as userActive,
            N'عذرا لايوجد حساب مسجل بالنظام لصاحب الهوية رقم : '+@UserID as Message_
                
     END
    
    

     ELSE
           
      begin

      

    if(select Count(*) From DATACORE.dbo.[User] uu where uu.userID = @GeneralNo and uu.userActive = 1) > 0
                begin



                if(select Count(*) FROM dbo.[User] u
                    LEFT JOIN @DepartmentInfo d ON u.userID = d.UserID
                	LEFT JOIN @ThameInfo t ON u.userID = t.UserID
                    WHERE u.userID = @GeneralNo and u.userActive = 1 and d.IdaraID is not null and d.DepartmentName is not null) > 0
                    begin

                    if(@PasswordResult = 1)
                    begin
                    SELECT 
                        CONCAT_WS(' ', u.fristName_A, u.secondName_A, u.lastName_A) AS fullName, 
                        u.userID,
                		d.IdaraID,
                        d.DepartmentName,
                        (SELECT Photo FROM DATACORE.dbo.UserPhoto up WHERE up.userID_FK = @GeneralNo) AS Photo,
                		case when t.ThameName is null Then 'default'
                		else t.ThameName
                		END ThameName,
                		d.DeptCode,
                        u.IDNumber,
                        u.userActive,
                        N'مرحبا بك : '+CONCAT_WS(' ', u.fristName_A, u.secondName_A, u.lastName_A)+N' تم تسجيل دخولك للنظام في '+convert(nvarchar(50),GETDATE(),111)+N' الساعة ' +convert(nvarchar(50),GETDATE(),108) as Message_
                    FROM dbo.[User] u
                    LEFT JOIN @DepartmentInfo d ON u.userID = d.UserID
                	LEFT JOIN @ThameInfo t ON u.userID = t.UserID
                    WHERE u.userID = @GeneralNo and u.userActive = 1

                    end
                else if(@PasswordResult = 0)
                    begin

                     select
                        0 as userActive,
                         N'عذرا اسم المستخدم او كلمة المرور غير صحيحة' as Message_
                    end

                    else if(@PasswordResult = 3)
                    begin

                     select
                        
                        0 as userActive,
                         N'يجب اعادة ضبط كلمة المرور للمستخدم' as Message_
                    end


                    end
                  else
                     begin

                     select
                        0 as userActive,
                        N'عذرا يوجد خطأ بالهيكل الاداري لصاحب الهوية رقم : '+@UserID as Message_

                     end
                END
           ELSE
                BEGIN
                      select
                        0 as userActive,
                        N'عذرا لايوجد حساب نشط بالنظام لصاحب الهوية رقم : '+@UserID as Message_

                
                END

    end     
    
END;
GO


-- اضافة باسورد جديد للمستخدم او تحديثه

USE [DATACORE]
GO

CREATE OR ALTER PROCEDURE dbo.SetUserPassword
(
    @UserID          NVARCHAR(20),     -- الرقم العام / رقم المستخدم
    @PlainPassword   NVARCHAR(200),    -- كلمة المرور الجديدة
    @entryData       NVARCHAR(20),     -- المستخدم الذي قام بالتعديل
    @hostName        NVARCHAR(200) = NULL
)
AS
BEGIN
    SET NOCOUNT ON;

    DECLARE @Salt       VARBINARY(32);
    DECLARE @Hash       VARBINARY(64);

    DECLARE @PrevSalt   VARBINARY(32);
    DECLARE @PrevHash   VARBINARY(64);
    DECLARE @Candidate  VARBINARY(64);

    BEGIN TRY

        ----------------------------------------------------
        -- 0) التحقق أن المستخدم موجود وفعال في جدول [User]
        ----------------------------------------------------
        IF NOT EXISTS (
            SELECT 1 
            FROM DATACORE.dbo.[User]
            WHERE IDNumber = @UserID
              AND userActive = 1
        )
        BEGIN
            SELECT 0 AS IsSuccessful,
                   N'المستخدم غير موجود أو غير فعّال في النظام.' AS Message_;
            RETURN;
        END


        ----------------------------------------------------
        -- 1) التحقق من تعقيد كلمة المرور الجديدة
        ----------------------------------------------------
        IF LEN(@PlainPassword) < 8
           OR @PlainPassword NOT LIKE '%[0-9]%'      -- لا تحتوي رقم
           OR @PlainPassword NOT LIKE '%[A-Za-z]%'   -- لا تحتوي حرف إنجليزي
        BEGIN
            SELECT 0 AS IsSuccessful,
                   N'كلمة المرور غير مقبولة. يجب أن لا تقل عن 8 خانات وتحتوي على حروف إنجليزية وأرقام.' AS Message_;
            RETURN;
        END


        ----------------------------------------------------
        -- 2) التأكد أن كلمة المرور الجديدة ليست نفسها القديمة
        ----------------------------------------------------
        SELECT TOP (1)
            @PrevSalt = PasswordSalt,
            @PrevHash = PasswordHash
        FROM dbo.userPassword
        WHERE IDNumber_FK = @UserID
          AND ISNULL(userPasswordActive, 1) = 1
        ORDER BY userPasswordStartDate DESC, userPasswordID DESC;

        IF @PrevHash IS NOT NULL
        BEGIN
            SET @Candidate = HASHBYTES(
                                'SHA2_256',
                                @PrevSalt + CAST(@PlainPassword AS VARBINARY(200))
                             );

            IF @Candidate = @PrevHash
            BEGIN
                SELECT 0 AS IsSuccessful,
                       N'لا يمكن استخدام نفس كلمة المرور السابقة. الرجاء اختيار كلمة مرور جديدة مختلفة.' AS Message_;
                RETURN;
            END
        END


        ----------------------------------------------------
        -- 3) البدء في المعاملة
        ----------------------------------------------------
        BEGIN TRANSACTION;

        -- تعطيل كل كلمات المرور السابقة
        UPDATE dbo.userPassword
        SET 
            userPasswordActive  = 0,
            userPasswordEndDate = CAST(GETDATE() AS DATE)
        WHERE IDNumber_FK = @UserID
          AND ISNULL(userPasswordActive, 1) = 1;


        ----------------------------------------------------
        -- 4) إنشاء Salt جديد + Hash جديد
        ----------------------------------------------------
        SET @Salt = CRYPT_GEN_RANDOM(32);

        SET @Hash = HASHBYTES(
                        'SHA2_256',
                        @Salt + CAST(@PlainPassword AS VARBINARY(200))
                    );


        ----------------------------------------------------
        -- 5) إدخال كلمة المرور الجديدة
        ----------------------------------------------------
        INSERT INTO dbo.userPassword
        (
            IDNumber_FK,
            PasswordHash,
            PasswordSalt,
            HashAlgorithm,
            userPasswordStartDate,
            userPasswordActive,
            entryDate,
            entryData,
            hostName
        )
        VALUES
        (
            @UserID,
            @Hash,
            @Salt,
            'SHA2_256',
            CAST(GETDATE() AS DATE),
            1,
            GETDATE(),
            @entryData,
            ISNULL(@hostName, HOST_NAME())
        );

        COMMIT TRANSACTION;

        SELECT 1 AS IsSuccessful,
               N'تم تحديث كلمة المرور / إنشاؤها بنجاح.' AS Message_;


    END TRY
    BEGIN CATCH

        IF @@TRANCOUNT > 0
            ROLLBACK TRANSACTION;

        SELECT 0 AS IsSuccessful,
               N'حصل خطأ أثناء تنفيذ العملية: ' + ERROR_MESSAGE() AS Message_;
    END CATCH
END
GO
